// Gradle plugins
buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'minecrell-snapshots'
            url = 'http://repo.minecrell.net/snapshots'
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        // VanillaGradle contains a custom ForgeGradle extension for SpongeCommon (see below)
        classpath 'net.minecrell:VanillaGradle:2.0.3-SNAPSHOT'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0') {
            exclude module: 'guava' // Older version of Guava conflicts with ForgeGradle
        }
        classpath 'org.spongepowered:spongegradle:0.2-SNAPSHOT'
    }
}

// Apply our custom ForgeGradle extension for SpongeCommon
// Compared to the regular tweaker-server plugin, SpongeCommon has all run tasks disabled
// and the Minecraft workspace defined as "provided" so they won't conflict with the implementation
apply plugin: 'net.minecrell.vanilla.server.library'
ext.minecraftPlugin = plugins.getPlugin('net.minecrell.vanilla.server.library')
ext.DIR_MCP_MAPPINGS = net.minecraftforge.gradle.common.Constants.DIR_MCP_MAPPINGS

// Apply shared ForgeGradle configuration
ext.common = project
apply from: 'gradle/minecraft.gradle'

// Inherit SpongeCommon version from SpongeAPI
version = api.version

minecraft {
    version = '1.8.9'
}

dependencies {
    // TODO: trove4j was removed in 1.8.8
    compile 'net.sf.trove4j:trove4j:3.0.3'

    compile api
    compile('org.spongepowered:mixin:0.4.7-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }

    compile 'com.zaxxer:HikariCP:2.4.3'

    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
    runtime 'org.mariadb.jdbc:mariadb-java-client:1.3.3'
    runtime 'com.h2database:h2:1.4.190'
    runtime 'org.xerial:sqlite-jdbc:3.8.11.2'
}

apply plugin: 'org.spongepowered.gradle'

sortAccessTransformers {
    add sourceSets.main
}
